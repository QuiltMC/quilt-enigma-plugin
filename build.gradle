plugins {
	id 'java'
	id 'maven-publish'
	id 'org.quiltmc.gradle.licenser' version '2.0.1'
}

version '1.3.2'
group 'org.quiltmc'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = version + (System.getenv("GITHUB_ACTIONS") ? "" : "+local")

repositories {
	mavenCentral()
	maven {
		name 'Quilt Repository'
		url 'https://maven.quiltmc.org/repository/release'
	}

	// Enigma dependencies
	maven {
		url 'https://maven.fabricmc.net/'
		content {
			includeModule('net.fabricmc', 'cfr')
		}
	}
	maven {
		// Vineflower snapshots
		url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
	}
}

dependencies {
	implementation "org.ow2.asm:asm:9.4"
	implementation "org.ow2.asm:asm-commons:9.4"
	implementation "org.ow2.asm:asm-tree:9.4"
	implementation "org.ow2.asm:asm-util:9.4"
	implementation "org.quiltmc:enigma:${enigma_version}"
	implementation "org.quiltmc:quilt-json5:1.0.1"
	implementation "org.jetbrains:annotations:23.0.0"

	testImplementation "org.quiltmc:enigma-swing:${enigma_version}"
}

license {
	rule file('codeformat/FABRIC_MODIFIED_HEADER')
	rule file('codeformat/HEADER')

	include '**/*.java'
}

java {
	withSourcesJar()
}

publishing {
	def ENV = System.getenv()
	publications {
		maven(MavenPublication) {
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_URL) {
			maven {
				url = ENV.MAVEN_URL

				credentials {
					username = ENV.MAVEN_USERNAME
					password = ENV.MAVEN_PASSWORD
				}
			}
		}
		mavenLocal()
	}
}
